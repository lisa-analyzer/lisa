dependencies {
	// utils
    api 'commons-io:commons-io:2.8.0'
    api 'org.apache.commons:commons-lang3:3.9'
    api 'org.apache.commons:commons-text:1.10.0'
	api 'org.apache.commons:commons-collections4:4.4'
	
	// json serialization/deserialization
	api 'com.fasterxml.jackson.core:jackson-databind:2.14.0'

	// dot file manipulation
	api 'org.graphstream:gs-core:2.0'
	
	// html templating
    api 'org.thymeleaf:thymeleaf:3.0.15.RELEASE'
    
	// logging
	api 'org.apache.logging.log4j:log4j-api:2.17.1'
	runtimeOnly 'org.apache.logging.log4j:log4j-core:2.17.1'
	runtimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.1'
	
    // reflective lookup
	api 'org.reflections:reflections:0.9.12'
	
	// time handling
	api 'joda-time:joda-time:2.10.14'
	
	// testing
    testImplementation 'junit:junit:4.12'
	testImplementation 'org.reflections:reflections:0.9.12'
}

tasks.register('generateVersionInfo', Copy) {
    description = 'Generates VersionInfo.java'
    group = 'build'

	from "$projectDir"
    into "$projectDir/src/main/java/it/unive/lisa"
    include('VersionInfo.template')
	rename ('VersionInfo.template', 'VersionInfo.java')
	filter { line -> line.replaceAll("@PRJ_VERSION@", project.version) }
}

compileJava.dependsOn generateVersionInfo
sourcesJar.dependsOn generateVersionInfo

