checkstyle {
    configFile = file(new File(rootProject.projectDir, 'checkstyle-config.xml'))
    sourceSets = []
    showViolations = true
    toolVersion '8.38' 
}

checkstyleTest.enabled = false

checkstyleMain {
    finalizedBy 'checkstyleErrorMessage'
    reports {
        xml.required = false
        html.required = false
    }
}

task checkstyleErrorMessage {
    onlyIf {
        checkstyleMain.state.failure != null
    }
    doLast {
        logger.error('Checkstyle plugin thrown an error. This means that the javadoc is not correctly setup. Inspect console output to find problematic javadocs.')
        logger.error('To reproduce locally, execute \'./gradlew checkstyleMain\'')
    }
}

spotless {
    enforceCheck = false
    encoding 'UTF-8'
    lineEndings 'UNIX'
    java {
        // tabs, no spaces
        leadingSpacesToTabs()

        // keep imports clean 
        importOrder() 
        removeUnusedImports()

        // use the eclipse formatting with the one provided with the project
        eclipse().configFile(new File(rootProject.projectDir, 'spotless-formatting.xml'))

        target 'src/**/*.java'	
        // ignore generated code
        targetExclude '**/build/generated/**/*.java'
        targetExclude '**/build/generated-src/**/*.java'
        targetExclude '**/target/generated-sources/**/*.java'
        targetExclude '**/VersionInfo.java'
    }
}

spotlessJava {
    // declaring explicit dependencies
    dependsOn 'compileJava', 'compileTestJava', 'processTestResources'
}

spotlessJavaCheck { 
    finalizedBy 'spotlessErrorMessage'
}

task spotlessErrorMessage {
    onlyIf {
        spotlessJavaCheck.state.failure != null
    }
    doLast {
        logger.error('Spotless plugin thrown an error. This means that the code is not correctly formatted.')
        logger.error('To reproduce locally, execute \'./gradlew spotlessCheck\'')
        logger.error('To automatically fix all the problems, execute \'./gradlew spotlessApply\'')
    }
}

tasks.register('checkCodeStyle') {
    group = 'verification'
    description = 'Execute spotless check and checkstyle to ensure code and javadoc formatting'
    dependsOn 'spotlessCheck', 'checkstyleMain', 'checkstyleTest'
}

tasks.register('applyCodeStyle') {
    group = 'verification'
    description = 'Execute spotless apply and checkstyle to apply code and javadoc formatting'
    dependsOn 'spotlessApply', 'checkstyleMain', 'checkstyleTest'
}
